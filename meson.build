project(
    'squeekboard',
    'c', 'rust',
    version: '1.0.10',
    license: 'GPLv3',
    meson_version: '>=0.49.0',
    default_options: [ 'warning_level=1', 'buildtype=debugoptimized', 'c_std=gnu11' ],
)

i18n = import('i18n')

conf_data = configuration_data()

if get_option('buildtype').startswith('debug')
    add_project_arguments('-DDEBUG=1', language : 'c')
endif
if get_option('buildtype') != 'plain'
    add_project_arguments('-fstack-protector-strong', language: 'c')
endif

prefix = get_option('prefix')
datadir = join_paths(prefix, get_option('datadir'))
pkgdatadir = join_paths(datadir, meson.project_name())
if get_option('depdatadir') == ''
  depdatadir = datadir
else
  depdatadir = get_option('depdatadir')
endif
dbusdir = join_paths(depdatadir, 'dbus-1/interfaces')

gsound_dep = dependency('gsound', required : false)
if gsound_dep.found()
  conf_data.set10('HAVE_GSOUND', true)
endif

summary = [
        '',
        '------------------',
        'squeekboard @0@'.format(meson.project_version()),
        '',
        '   gsound: @0@'.format(conf_data.get('HAVE_GSOUND', false)),
        '------------------',
        ''
]
message('\n'.join(summary))

subdir('data')
subdir('protocols')
subdir('eek')
subdir('src')
subdir('po')
